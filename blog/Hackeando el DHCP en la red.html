<html>
<head>
<title>Hackeando el DHCP en la red</title>
<link rel="stylesheet" type="text/css" href="css/blogstyle.css">
</head>
<body style="margin:20;padding:0;">
<script language="javascript">
window.parent.document.title = "Hackeando el DHCP en la red";
</script>
<div id="section">



<form action="https://www.paypal.com/cgi-bin/webscr" method="post" target="_top">
<input type="hidden" name="cmd" value="_s-xclick">
<input type="hidden" name="hosted_button_id" value="7HY2N4VFEWYCL">
<input type="image" src="https://www.paypalobjects.com/en_US/i/btn/btn_donate_SM.gif" border="0" name="submit" alt="PayPal - The safer, easier way to pay online!">
¿ Te sirvió la data ? ¿ Te pareció interesante ? Con tu aporte puedo continuar. <img alt="" border="0" src="https://www.paypalobjects.com/en_US/i/scr/pixel.gif" width="1" height="1">
</form>

<hr>
<p id="postTitle"><a href="Hackeando%20el%20DHCP%20en%20la%20red.html" id="alink">Hackeando el DHCP en la red</a></p>

Para bien o para mal, en casa tengo un proveedor de televisión por cable.  Es el servicio
tradicional donde llega un cable coaxial y se conecta al televisor.  Hace unos pocos dias
vinieron e instalaron un Decodificador, es un dispositivo que se conecta entre el cable
coaxial y la tele permitiendo ver señales digitales con mejor calidad, también incluye un
software con Youtube y aplicaciones para mirar televisión por Internet.<br /><br />

<b>El problema</b>: Este decodificador se conecta a la red LAN de la casa, pero no permite
configurarle una dirección IP estática, la única manera es que obtenga una IP, dirección
de máscara de red y servidores 
de resolución de nombres por <a href="https://www.ietf.org/rfc/rfc2131.txt">DHCP</a>.
<br /><br />

El verdadero problema es que me tomo muy en serio todo lo que permito y no permito
en mi red hogareña, cada servicio nuevo es algo que me preocupa.  Quería evitar tener
que instalar un servidor de DHCP, claramente está deshabilitado el DHCP en mi servidor
wifi por que hago funcionar a las máquinas con IP estática (hasta ahora).<br /><br />

<u>¿ Cómo evitar instalar un servidor de DHCP y hacer funcionar el decodificador que necesita
una IP ?  No hacerlo funcionar no es una opción.</u>
<br />
<b>Solucion:</b> Crear un servidor DHCP minimalista de menos de 200 lineas, 
un script en lo posible, bien simple de levantar y cerrar y poder controlar que IP asigno
a cada estación.<br /><br />


En el RFC se describe el funcionamiento de este protocolo, un gráfico muy claro es este, nos
va a servir luego para revisar el funcionamiento del programa.<br />

<pre>


                Server          Client          Server
            (not selected)                    (selected)

                  v               v               v
                  |               |               |
                  |     Begins initialization     |
                  |               |               |
                  | _____________/|\____________  |
                  |/DHCPDISCOVER | DHCPDISCOVER  \|
                  |               |               |
              Determines          |          Determines
             configuration        |         configuration
                  |               |               |
                  |\             |  ____________/ |
                  | \________    | /DHCPOFFER     |
                  | DHCPOFFER\   |/               |
                  |           \  |                |
                  |       Collects replies        |
                  |             \|                |
                  |     Selects configuration     |
                  |               |               |
                  | _____________/|\____________  |
                  |/ DHCPREQUEST  |  DHCPREQUEST\ |
                  |               |               |
                  |               |     Commits configuration
                  |               |               |
                  |               | _____________/|
                  |               |/ DHCPACK      |
                  |               |               |
                  |    Initialization complete    |
                  |               |               |
                  .               .               .
                  .               .               .
                  |               |               |
                  |      Graceful shutdown        |
                  |               |               |
                  |               |\ ____________ |
                  |               | DHCPRELEASE  \|
                  |               |               |
                  |               |        Discards lease
                  |               |               |
                  v               v               v
     Figure 3: Timeline diagram of messages exchanged between DHCP
               client and servers when allocating a new network address

</pre>

<br />




Script server:<br />
<pre>
#!/usr/bin/perl
use strict;

use IO::Socket;
use Net::DHCP::Packet;
use Net::DHCP::Constants;

######## La ip del server:
my $server_ip = "10.10.10.176";
my $gateway = "10.10.10.1";
my $dnserver = "8.8.8.8";
my $subnet_mask = "255.255.255.255";

my %mac_a_ip = ();

# Decodificador
$mac_a_ip{"b0b88f232fc900000000000000000000"} = "10.10.10.4";
# VM 
$mac_a_ip{"080997ddbbf100000000000000000000"} = "10.10.10.5";

my $socket_in = IO::Socket::INET->new(
	LocalPort => 67,
	LocalAddr => "255.255.255.255",
	Proto    => 'udp') or die $@;

while(1) {
	my $buf;
	print "Waiting...";
	$socket_in->recv($buf,4096);
	my $packet = new Net::DHCP::Packet($buf);
	my $messagetype = $packet->getOptionValue(DHO_DHCP_MESSAGE_TYPE());
	print "Received:\n";
	print $packet->toString();

	if ($messagetype eq DHCPDISCOVER()) {
		send_offer($packet);
	} elsif ($messagetype eq DHCPREQUEST()) {
		$packet->comment(1);
		send_ack($packet);
	}
}

sub send_offer {
	my($request)=@_;
	print "-------- Envio mi oferta ---------\n";
	my $which_machine = $request->chaddr;
	my $client_ip = $mac_a_ip{"$which_machine"};
	print "A $which_machine Ofrezco IP: $client_ip [Servidor: $server_ip:67]\n";
	my $socket_out = IO::Socket::INET->new(
		PeerPort => 68,
		PeerAddr => "255.255.255.255",
		LocalAddr => "$server_ip:67",
		Broadcast => 1,
		Proto    => 'udp') or die $@;

	my $offer = new Net::DHCP::Packet(
                                     Op => BOOTREPLY(),
                                     Xid => $request->xid(),
                                     Flags => $request->flags(),
                                     Ciaddr => $request->ciaddr(),
                                     Yiaddr => $client_ip,
                                     Siaddr => $server_ip,
                                     Giaddr => $request->giaddr(),
                                     Chaddr => $request->chaddr(),
                                     DHO_DHCP_MESSAGE_TYPE() => DHCPOFFER(),
                                   );
	$offer->addOptionValue(DHO_DHCP_LEASE_TIME, 84600);
	$offer->addOptionValue(DHO_DOMAIN_NAME_SERVERS, $dnserver);
	$offer->addOptionValue(DHO_ROUTERS, $gateway);
	$offer->addOptionValue(DHO_SUBNET_MASK(), $subnet_mask);
	$offer->addOptionValue(DHO_NAME_SERVERS, $server_ip);
	$offer->addOptionValue(DHO_DHCP_LEASE_TIME, 100);
	print $offer->toString();
	$socket_out->send($offer->serialize()) or die $!;
}

sub send_ack {
	my ($request) = @_;
	print "-------- Envio ACK ---------\n";
	my $which_machine = $request->chaddr;
	my $client_ip = $mac_a_ip{"$which_machine"};
	print "A $which_machine ACK de IP: $client_ip [Servidor: $server_ip:67]\n";
	my $socket_out = IO::Socket::INET->new(
		PeerPort => 68,
		PeerAddr => "255.255.255.255",
		LocalAddr => "$server_ip:67",
		Broadcast => 1,
		Proto    => 'udp') or die $@;

	$request = Net::DHCP::Packet->new(
            Comment                 => $request->comment(),
            Op                      => BOOTREPLY(),
            Hops                    => $request->hops(),
            Xid                     => $request->xid(),
            Flags                   => $request->flags(),
            Ciaddr                  => $request->ciaddr(),
            Yiaddr                  => $client_ip,
            Siaddr                  => $request->siaddr(),
            Giaddr                  => $request->giaddr(),
            Chaddr                  => $request->chaddr(),
            DHO_DHCP_MESSAGE_TYPE() => DHCPACK(),
        );

	$request->addOptionValue(DHO_DHCP_LEASE_TIME, 84600);
	$request->addOptionValue(DHO_DOMAIN_NAME_SERVERS, $dnserver);
	$request->addOptionValue(DHO_SUBNET_MASK(), $subnet_mask);
	$request->addOptionValue(DHO_ROUTERS, $gateway);
	$socket_out->send($request->serialize()) or die $!;
	print STDERR "send ack\n";
}

</pre>
<br /><br />
Ahora revisando el funcionamiento del programa:
<br /><br />
Ejecutamos el servidorcito dhcpd.pl<br />

<pre>
Waiting...Received:
op = BOOTREQUEST
htype = HTYPE_ETHER
hlen = 6
hops = 0
xid = 829e755
secs = 2816
flags = 0
ciaddr = 10.10.10.55
yiaddr = 0.0.0.0
siaddr = 0.0.0.0
giaddr = 0.0.0.0
chaddr = 080027ddbbf1
sname = 
file = 
Options : 
 DHO_DHCP_MESSAGE_TYPE(53) = DHCPRELEASE
 DHO_DHCP_SERVER_IDENTIFIER(54) = 255.255.255.255
 DHO_DHCP_CLIENT_IDENTIFIER(61) = \x01\x08\x00'\xDD\xBB\xF1
padding [41] = 0000000000000000000000000000000000000000000000000000000000000000000000000000000000
Waiting...Received:
op = BOOTREQUEST
htype = HTYPE_ETHER
hlen = 6
hops = 0
xid = f32fb34e
secs = 0
flags = 8000
ciaddr = 0.0.0.0
yiaddr = 0.0.0.0
siaddr = 0.0.0.0
giaddr = 0.0.0.0
chaddr = 080027ddbbf1
sname = 
file = 
Options : 
 DHO_DHCP_MESSAGE_TYPE(53) = DHCPDISCOVER
 DHO_DHCP_CLIENT_IDENTIFIER(61) = \x01\x08\x00'\xDD\xBB\xF1
 DHO_DHCP_REQUESTED_ADDRESS(50) = 10.10.10.55
 DHO_HOST_NAME(12) = IBM620-0
 DHO_VENDOR_CLASS_IDENTIFIER(60) = MSFT 5.0
 DHO_DHCP_PARAMETER_REQUEST_LIST(55) = 1 15 3 6 44 46 47 31 33 121 249 43
padding [7] = 00000000000000
</pre>

<br />
Aca le envio la IP ofrecida, por la MAC en el script se programa una IP.
<br />

<pre>
-------- Envio mi oferta ---------
A 080027ddbbf100000000000000000000 Ofrezco IP: 10.10.10.55 [Servidor: 10.10.10.176:67]
op = BOOTREPLY
htype = HTYPE_ETHER
hlen = 6
hops = 0
xid = f32fb34e
secs = 0
flags = 8000
ciaddr = 0.0.0.0
yiaddr = 10.10.10.55
siaddr = 10.10.10.176
giaddr = 0.0.0.0
chaddr = 080027ddbbf1
sname = 
file = 
Options : 
 DHO_DHCP_MESSAGE_TYPE(53) = DHCPOFFER
 DHO_DHCP_LEASE_TIME(51) = 100
 DHO_DOMAIN_NAME_SERVERS(6) = 8.8.8.8
 DHO_ROUTERS(3) = 10.10.10.1
 DHO_SUBNET_MASK(1) = 255.255.255.255
 DHO_NAME_SERVERS(5) = 10.10.10.176
 DHO_DHCP_LEASE_TIME(51) = 100
padding [0] = 
Waiting...Received:
op = BOOTREQUEST
htype = HTYPE_ETHER
hlen = 6
hops = 0
xid = f32fb34e
secs = 0
flags = 8000
ciaddr = 0.0.0.0
yiaddr = 0.0.0.0
siaddr = 0.0.0.0
giaddr = 0.0.0.0
chaddr = 080027ddbbf1
sname = 
file = 
Options : 
 DHO_DHCP_MESSAGE_TYPE(53) = DHCPREQUEST
 DHO_DHCP_CLIENT_IDENTIFIER(61) = \x01\x08\x00'\xDD\xBB\xF1
 DHO_DHCP_REQUESTED_ADDRESS(50) = 10.10.10.55
 DHO_HOST_NAME(12) = IBM620-0
 DHO_FQDN(81) = \x00\x00\x00IBM620-0
 DHO_VENDOR_CLASS_IDENTIFIER(60) = MSFT 5.0
 DHO_DHCP_PARAMETER_REQUEST_LIST(55) = 1 15 3 6 44 46 47 31 33 121 249 43
padding [0] = 
</pre>

<br />
Aca se le envia un "ACK" para finalizar la transacción.
<br />
<br />
<pre>
-------- Envio ACK ---------
A 080027ddbbf100000000000000000000 ACK de IP: 10.10.10.55 [Servidor: 10.10.10.176:67]
send ack
Waiting...Received:
op = BOOTREQUEST
htype = HTYPE_ETHER
hlen = 6
hops = 0
xid = 1973a052
secs = 0
flags = 8000
ciaddr = 10.10.10.55
yiaddr = 0.0.0.0
siaddr = 0.0.0.0
giaddr = 0.0.0.0
chaddr = 080027ddbbf1
sname = 
file = 
Options : 
 DHO_DHCP_MESSAGE_TYPE(53) = DHCPINFORM
 DHO_DHCP_CLIENT_IDENTIFIER(61) = \x01\x08\x00'\xDD\xBB\xF1
 DHO_HOST_NAME(12) = IBM620-0
 DHO_VENDOR_CLASS_IDENTIFIER(60) = MSFT 5.0
 DHO_DHCP_PARAMETER_REQUEST_LIST(55) = 1 15 3 6 44 46 47 31 33 121 249 43 252
padding [12] = 000000000000000000000000
Waiting...Received:
op = BOOTREQUEST
htype = HTYPE_ETHER
hlen = 6
hops = 0
xid = 1973a052
secs = 768
flags = 8000
ciaddr = 10.10.10.55
yiaddr = 0.0.0.0
siaddr = 0.0.0.0
giaddr = 0.0.0.0
chaddr = 080027ddbbf1
sname = 
file = 
Options : 
 DHO_DHCP_MESSAGE_TYPE(53) = DHCPINFORM
 DHO_DHCP_CLIENT_IDENTIFIER(61) = \x01\x08\x00'\xDD\xBB\xF1
 DHO_HOST_NAME(12) = IBM620-0
 DHO_VENDOR_CLASS_IDENTIFIER(60) = MSFT 5.0
 DHO_DHCP_PARAMETER_REQUEST_LIST(55) = 1 15 3 6 44 46 47 31 33 121 249 43 252
padding [12] = 000000000000000000000000
</pre>
<br />

<a href="https://github.com/walt3rminer/perl/tree/master/dhcpserver" target="_new">Fuente de este post</a><br />


<b>Published: Thursday, December 29, 2016</b>
<a href="tag_security">security</a>&nbsp;<a href="tag_linux">linux</a>&nbsp;<a href="tag_perl">perl</a>&nbsp;
</div>

<hr>
</body></html>